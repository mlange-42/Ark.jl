
"""
    const zero_entity::Entity

The reserved zero [`Entity`](@ref) value.
Can be used to represent "no entity"/"nil".
"""
const zero_entity::Entity = _new_entity(1, 0)

"""
    World{CS<:Tuple,CT<:Tuple,ST<:Tuple,N,M}

The World is the central storage for [entities](@ref Entities),
[components](@ref Components) and [resources](@ref Resources).

See the constructor [World](@ref World(::Union{Type,Pair}...; ::Int, ::Bool)) for details.
"""
mutable struct World{CS<:Tuple,CT<:Tuple,ST<:Tuple,N,M} <: _AbstractWorld
    const _entities::Vector{_EntityIndex}
    const _storages::CS
    const _relations::Vector{_ComponentRelations}
    const _archetypes::Vector{_Archetype{M}}
    const _tables::Vector{_Table}
    const _index::_ComponentIndex{M}
    const _registry::_ComponentRegistry
    const _entity_pool::_EntityPool
    const _lock::_Lock
    const _graph::_Graph{M}
    const _resources::Dict{DataType,Any}
    const _event_manager::_EventManager{World{CS,CT,ST,N,M},M}
    const _temp_relations::Vector{Pair{Int,Entity}}
    const _initial_capacity::Int
end

"""
    World(
        comp_types::Type...;
        initial_capacity::Int=128,
        allow_mutable::Bool=false,
    )

Creates a new, empty [`World`](@ref) for the given component types.

All component types that will be used with the world must be specified.
This allows Ark to use Julia's compile-time method generation to achieve the best performance.

For each component type, an individual [storage mode](@ref component-storages) can be set.
See also [VectorStorage](@ref) and [StructArrayStorage](@ref).

Additional arguments can be used to allow mutable component types (forbidden by default and discouraged)
and an initial capacity for entities in [archetypes](@ref Architecture).

# Arguments

  - `comp_types`: The component types used by the world.
  - `initial_capacity`: Initial capacity for entities in each archetype and in the entity index.
  - `allow_mutable`: Allows mutable components. Use with care, as all mutable objects are heap-allocated in Julia.

# Examples

A World where all components use the default storage mode:

```jldoctest; setup = :(using Ark; include(string(dirname(pathof(Ark)), "/docs.jl"))), output = false
world = World(
    Position,
    Velocity,
)

# output

World(entities=0, comp_types=(Position, Velocity))
```

A World with individually configured storage modes:

```jldoctest; setup = :(using Ark; include(string(dirname(pathof(Ark)), "/docs.jl"))), output = false
world = World(
    Position => StructArrayStorage,
    Velocity => StructArrayStorage,
    Health => VectorStorage,
)

# output

World(entities=0, comp_types=(Position, Velocity, Health))
```
"""
function World(comp_types::Union{Type,Pair{<:Type,<:Type}}...; initial_capacity::Int=128, allow_mutable=false)
    types = map(arg -> arg isa Type ? arg : arg.first, comp_types)
    storages = map(arg -> arg isa Type ? VectorStorage : arg.second, comp_types)

    _World_from_types(Val{Tuple{types...}}(), Val{Tuple{storages...}}(), Val(allow_mutable), initial_capacity)
end

@generated function _component_id(::Type{CS}, ::Type{C})::Int where {CS<:Tuple,C}
    for (i, S) in enumerate(CS.parameters)
        if S <: _ComponentStorage && S.parameters[1] === C
            return :($i)
        end
    end
    return :(throw(ArgumentError(lazy"Component type $C not found in the World")))
end

@generated function _get_storage(world::World{CS}, ::Type{C}) where {CS<:Tuple,C}
    storage_types = CS.parameters
    for (i, S) in enumerate(storage_types)
        if S <: _ComponentStorage && S.parameters[1] === C
            return :(world._storages.$i)
        end
    end
    return :(throw(ArgumentError(lazy"Component type $C not found in the World")))
end

@generated function _get_relations(world::World{CS}, ::Type{C}) where {CS<:Tuple,C}
    storage_types = CS.parameters
    for (i, S) in enumerate(storage_types)
        if S <: _ComponentStorage && S.parameters[1] === C
            return :(world._relations[$i])
        end
    end
    return :(throw(ArgumentError(lazy"Component type $C not found in the World")))
end

@inline function _find_or_create_archetype!(
    world::World,
    start::_GraphNode,
    add::Tuple{Vararg{Int}},
    remove::Tuple{Vararg{Int}},
)::UInt32
    node = _find_node(world._graph, start, add, remove)

    archetype = (node.archetype == typemax(UInt32)) ?
                _create_archetype!(world, node) :
                node.archetype

    return archetype
end

@inline function _find_or_create_table!(
    world::World,
    old_table::_Table,
    add::Tuple{Vararg{Int}},
    remove::Tuple{Vararg{Int}},
    relations::Tuple{Vararg{Int}},
    targets::Tuple{Vararg{Entity}},
)::UInt32
    old_arch = world._archetypes[old_table.archetype]
    new_arch_index = _find_or_create_archetype!(world, old_arch.node, add, remove)
    new_arch = world._archetypes[new_arch_index]

    # Find existing relations that were not removed, and add new relations.
    all_relations = world._temp_relations
    requires_copy = true
    if _has_relations(old_table) || !isempty(relations)
        if length(remove) > 0
            for rel in old_table.relations
                if _get_bit(new_arch.mask, rel[1])
                    push!(all_relations, rel)
                end
            end
            for i in eachindex(relations)
                push!(all_relations, Pair(relations[i], targets[i]))
            end
        else
            if length(relations) > 0
                append!(all_relations, old_table.relations)
                for i in eachindex(relations)
                    push!(all_relations, Pair(relations[i], targets[i]))
                end
            else
                all_relations = old_table.relations
                requires_copy = false
            end
        end
    end

    new_table, found = _get_table(world, new_arch, all_relations)

    if found
        if requires_copy
            resize!(all_relations, 0)
        end
        return new_table.id
    end

    # TODO: ensure that relations are the same and in the same order as in the archetype
    if length(all_relations) > 0
        sort!(all_relations; by=first)
    end
    # TODO: recycle table
    if requires_copy
        new_table_id = _create_table!(world, new_arch, copy(all_relations))
        resize!(all_relations, 0)
        return new_table_id
    end

    return _create_table!(world, new_arch, all_relations)
end

function _create_table!(world::World, arch::_Archetype, relations::Vector{Pair{Int,Entity}})::UInt32
    if length(relations) < length(arch.relations)
        # TODO: check duplicates
        throw(ArgumentError("relation targets must be fully specified"))
    end
    # TODO: check that all components are relations
    # TODO: check that the archetype contains all components

    # TODO: recycle tables if available

    new_table_id = length(world._tables) + 1
    table = _new_table(UInt32(new_table_id), arch.id, world._initial_capacity, relations)
    push!(world._tables, table)

    _push_empty_to_all_storages!(world)

    for comp in arch.components
        _activate_new_column_for_comp!(world, comp, new_table_id)
        push!(world._index.components[comp], arch)
    end

    _add_table!(world._relations, arch, table)

    return UInt32(new_table_id)
end

function _create_archetype!(world::World, node::_GraphNode)::UInt32
    components = _active_bit_indices(node.mask)
    relations = Int[]
    for id in components
        if _is_relation(world._registry, id)
            push!(relations, id)
        end
    end

    arch =
        _Archetype(UInt32(length(world._archetypes) + 1), node, _TableIDs(), relations, components...)
    push!(world._archetypes, arch)

    index = length(world._archetypes)
    node.archetype = UInt32(index)

    _push_zero_to_all_relations!(world)

    for (i, comp) in enumerate(relations)
        _activate_relation_for_comp!(world, comp, index, i)
    end

    return UInt32(index)
end

function _get_table(world::World, arch::_Archetype, relations::Vector{Pair{Int,Entity}})::Tuple{_Table,Bool}
    if length(arch.tables) == 0
        return world._tables[1], false
    end
    if !_has_relations(arch)
        return world._tables[arch.tables[1]], true
    end
    return _get_table_slow_path(world, arch, relations)
end

function _get_table_slow_path(
    world::World,
    arch::_Archetype,
    relations::Vector{Pair{Int,Entity}},
)::Tuple{_Table,Bool}
    if length(relations) < length(arch.relations)
        # TODO: check duplicates
        throw(ArgumentError("relation targets must be fully specified"))
    end

    first_rel = relations[1]
    rel_comp = first_rel.first
    target_id = first_rel.second._id

    rel_idx = world._relations[rel_comp].indices[arch.id]
    index = arch.index[rel_idx]
    if !haskey(index, target_id)
        return world._tables[1], false
    end

    tables = index[target_id]
    if length(arch.relations) == 1
        return world._tables[tables.ids[1]], true
    end

    for table_id in tables.ids
        table = world._tables[table_id]
        if _matches_exact(world._relations, table, relations)
            return table, true
        end
    end

    return world._tables[1], false
end

function _get_tables(world::World, arch::_Archetype)::Vector{UInt32}
    if !_has_relations(arch)
        return arch.tables.ids
    end
    # TODO: implement relation index
    error("not implemented")
end

function _get_tables(world::World, arch::_Archetype, relations::Vector{Pair{Int,Entity}})::Vector{UInt32}
    if !_has_relations(arch) || isempty(relations)
        return arch.tables.ids
    end
    # TODO: implement relation index
    error("not implemented")
end

@inline function _create_entity!(world::World, table_index::UInt32)::Tuple{Entity,Int}
    _check_locked(world)

    entity = _get_entity(world._entity_pool)
    table = world._tables[table_index]
    archetype = world._archetypes[table.archetype]

    index = _add_entity!(table, entity)

    for comp in archetype.components
        _ensure_column_size_for_comp!(world, comp, table_index, index)
    end

    if entity._id > length(world._entities)
        push!(world._entities, _EntityIndex(table_index, UInt32(index)))
    else
        @inbounds world._entities[entity._id] = _EntityIndex(table_index, UInt32(index))
    end
    return entity, index
end

function _create_entities!(world::World, table_index::UInt32, n::UInt32)::Tuple{UInt32,UInt32}
    _check_locked(world)

    table = world._tables[Int(table_index)]
    archetype = world._archetypes[table.archetype]
    old_length = length(table.entities)
    new_length = old_length + n

    resize!(table, new_length)
    for i in (old_length+1):new_length
        entity = _get_entity(world._entity_pool)
        @inbounds table.entities._data[i] = entity

        if entity._id > length(world._entities)
            push!(world._entities, _EntityIndex(table_index, i))
        else
            @inbounds world._entities[Int(entity._id)] = _EntityIndex(table_index, i)
        end
    end

    for comp in archetype.components
        _ensure_column_size_for_comp!(world, comp, table_index, new_length)
    end

    return old_length + 1, new_length
end

function _move_entity!(world::World, entity::Entity, table_index::UInt32)::Int
    _check_locked(world)

    index = world._entities[entity._id]
    old_table = world._tables[index.table]
    new_table = world._tables[table_index]
    old_archetype = world._archetypes[old_table.archetype]
    new_archetype = world._archetypes[new_table.archetype]

    new_row = _add_entity!(new_table, entity)
    swapped = _swap_remove!(old_table.entities._data, index.row)

    # Move component data only for components present in old_archetype that are also present in new_archetype
    for comp in old_archetype.components
        if !_get_bit(new_archetype.mask, comp)
            continue
        end
        _move_component_data!(world, comp, index.table, table_index, index.row)
    end

    # Ensure columns in the new archetype have capacity to hold new_row for components of new_archetype
    for comp in new_archetype.components
        _ensure_column_size_for_comp!(world, comp, table_index, new_row)
    end

    if swapped
        swap_entity = old_table.entities[index.row]
        world._entities[swap_entity._id] = index
    end

    world._entities[entity._id] = _EntityIndex(table_index, UInt32(new_row))
    return new_row
end

function _copy_entity!(world::World, entity::Entity, mode::Val)::Entity
    _check_locked(world)

    index = world._entities[entity._id]
    new_entity, new_row = _create_entity!(world, index.table)
    table = world._tables[index.table]
    archetype = world._archetypes[table.archetype]

    for comp in archetype.components
        _copy_component_data!(world, comp, index.table, index.table, index.row, UInt32(new_row), mode)
    end

    world._entities[new_entity._id] = _EntityIndex(index.table, UInt32(new_row))

    if _has_observers(world._event_manager, OnCreateEntity)
        _fire_create_entity(world._event_manager, new_entity, archetype.mask)
    end
    return new_entity
end

@generated function _copy_entity!(
    world::W,
    entity::Entity,
    ::Val{ATS},
    add::Tuple,
    ::RTS,
    mode::CP,
)::Entity where {W<:World,ATS<:Tuple,RTS<:Tuple,CP<:Val}
    add_types = ATS.parameters
    rem_types = _to_types(RTS)
    exprs = []

    add_ids = tuple([_component_id(W.parameters[1], T) for T in add_types]...)
    rem_ids = tuple([_component_id(W.parameters[1], T) for T in rem_types]...)

    push!(exprs, :(index = world._entities[entity._id]))
    push!(exprs, :(old_table = world._tables[index.table]))
    push!(exprs, :(old_archetype = world._archetypes[old_table.archetype]))
    push!(
        exprs,
        :(
            new_table_index =
                _find_or_create_table!(
                    world, old_table, $add_ids, $rem_ids, (), (),
                )
        ),
    )
    push!(exprs, :(new_table = world._tables[new_table_index]))
    push!(exprs, :(new_archetype = world._archetypes[new_table.archetype]))

    push!(exprs, :(entity_and_row = _create_entity!(world, new_table_index)))
    push!(exprs, :(new_entity = entity_and_row[1]))
    push!(exprs, :(new_row = entity_and_row[2]))

    push!(
        exprs,
        :(
            for comp in old_archetype.components
                if !_get_bit(new_archetype.mask, comp)
                    continue
                end
                _copy_component_data!(world, comp, index.table, new_table_index, index.row, UInt32(new_row), mode)
            end
        ),
    )

    for i in 1:length(add_types)
        T = add_types[i]
        stor_sym = Symbol("stor", i)
        col_sym = Symbol("col", i)
        val_expr = :(add.$i)

        push!(exprs, :($stor_sym = _get_storage(world, $T)))
        push!(exprs, :(@inbounds $col_sym = $stor_sym.data[new_table_index]))
        push!(exprs, :(@inbounds $col_sym[new_row] = $val_expr))
    end

    push!(exprs, :(
        if _has_observers(world._event_manager, OnCreateEntity)
            _fire_create_entity(world._event_manager, new_entity, new_archetype.mask)
        end
    ))

    push!(exprs, Expr(:return, :new_entity))

    return quote
        @inbounds begin
            $(Expr(:block, exprs...))
        end
    end
end

"""
    remove_entity!(world::World, entity::Entity)

Removes an [`Entity`](@ref) from the [`World`](@ref).

# Example

```jldoctest; setup = :(using Ark; include(string(dirname(pathof(Ark)), "/docs.jl"))), output = false
remove_entity!(world, entity)

# output

```
"""
function remove_entity!(world::World, entity::Entity)
    if !is_alive(world, entity)
        throw(ArgumentError("can't remove a dead entity"))
    end
    _check_locked(world)

    index = world._entities[entity._id]
    table = world._tables[index.table]
    archetype = world._archetypes[table.archetype]

    if _has_observers(world._event_manager, OnRemoveEntity)
        l = _lock(world._lock)
        _fire_remove_entity(
            world._event_manager, entity,
            archetype.mask,
        )
        _unlock(world._lock, l)
    end

    swapped = _swap_remove!(table.entities._data, index.row)

    # Only operate on storages for components present in this archetype
    for comp in archetype.components
        _swap_remove_in_column_for_comp!(world, comp, index.table, index.row)
    end

    if swapped
        swap_entity = table.entities[index.row]
        world._entities[swap_entity._id] = index
    end

    _recycle(world._entity_pool, entity)
    return nothing
end

"""
    is_alive(world::World, entity::Entity)::Bool

Returns whether an [`Entity`](@ref) is alive.
"""
function is_alive(world::World, entity::Entity)::Bool
    return _is_alive(world._entity_pool, entity)
end

"""
    is_locked(world::World)::Bool

Returns whether the world is currently [locked](@ref world-lock) for modifications.
"""
function is_locked(world::World)::Bool
    return _is_locked(world._lock)
end

function _check_locked(world::World)
    if _is_locked(world._lock)
        throw(
            InvalidStateException(
                "cannot modify a locked world: collect entities into a vector and apply changes after query iteration has completed",
                :locked_world,
            ),
        )
    end
end

"""
    get_components(world::World, entity::Entity, comp_types::Tuple)

Get the given components for an [`Entity`](@ref).
Components are returned in a tuple.

# Example

```jldoctest; setup = :(using Ark; include(string(dirname(pathof(Ark)), "/docs.jl"))), output = false
pos, vel = get_components(world, entity, (Position, Velocity))

# output

(Position(0.0, 0.0), Velocity(0.0, 0.0))
```
"""
@inline Base.@constprop :aggressive function get_components(world::World, entity::Entity, comp_types::Tuple)
    if !is_alive(world, entity)
        throw(ArgumentError("can't get components of a dead entity"))
    end
    return @inline _get_components(world, entity, ntuple(i -> Val(comp_types[i]), length(comp_types)))
end

@generated function _get_components(world::World, entity::Entity, ::TS) where {TS<:Tuple}
    types = _to_types(TS)
    if length(types) == 0
        return :(())
    end

    exprs = Expr[]
    push!(exprs, :(@inbounds idx = world._entities[entity._id]))

    for i in 1:length(types)
        T = types[i]
        stor_sym = Symbol("stor", i)
        val_sym = Symbol("v", i)

        push!(exprs, :($(stor_sym) = _get_storage(world, $T)))
        push!(exprs, :($(val_sym) = _get_component($(stor_sym), idx.table, idx.row)))
    end

    vals = [:($(Symbol("v", i))) for i in 1:length(types)]
    push!(exprs, Expr(:return, Expr(:tuple, vals...)))

    return quote
        @inbounds begin
            $(Expr(:block, exprs...))
        end
    end
end

"""
    has_components(world::World, entity::Entity, comp_types::Tuple)::Bool

Returns whether an [`Entity`](@ref) has all given components.

# Example

```jldoctest; setup = :(using Ark; include(string(dirname(pathof(Ark)), "/docs.jl"))), output = false
has = has_components(world, entity, (Position, Velocity))

# output

true
```
"""
@inline Base.@constprop :aggressive function has_components(world::World, entity::Entity, comp_types::Tuple)
    if !is_alive(world, entity)
        throw(ArgumentError("can't check components of a dead entity"))
    end
    index = world._entities[entity._id]
    return @inline _has_components(world, index, ntuple(i -> Val(comp_types[i]), length(comp_types)))
end

@generated function _has_components(world::World, index::_EntityIndex, ::TS) where {TS<:Tuple}
    types = _to_types(TS)
    exprs = []

    for i in 1:length(types)
        T = types[i]
        stor_sym = Symbol("stor", i)
        col_sym = Symbol("col", i)

        push!(exprs, :($stor_sym = _get_storage(world, $T)))
        push!(exprs, :($col_sym = $stor_sym.data[index.table]))
        push!(exprs, :(
            if length($col_sym) == 0
                return false
            end
        ))
    end

    push!(exprs, :(return true))

    return quote
        @inbounds begin
            $(Expr(:block, exprs...))
        end
    end
end

"""
    set_components!(world::World, entity::Entity, values::Tuple)

Sets the given component values for an [`Entity`](@ref). Types are inferred from the values.
The entity must already have all these components.

# Example

```jldoctest; setup = :(using Ark; include(string(dirname(pathof(Ark)), "/docs.jl"))), output = false
set_components!(world, entity, (Position(0, 0), Velocity(1, 1)))

# output

```
"""
@inline Base.@constprop :aggressive function set_components!(world::World, entity::Entity, values::Tuple)
    if !is_alive(world, entity)
        throw(ArgumentError("can't set components of a dead entity"))
    end
    return @inline _set_components!(world, entity, Val{typeof(values)}(), values)
end

@generated function _set_components!(world::World, entity::Entity, ::Val{TS}, values::Tuple) where {TS<:Tuple}
    types = TS.parameters
    exprs = [:(@inbounds idx = world._entities[entity._id])]

    for i in 1:length(types)
        T = types[i]
        stor_sym = Symbol("stor", i)
        val_expr = :(values.$i)

        push!(exprs, :($stor_sym = _get_storage(world, $T)))
        push!(exprs, :(_set_component!($stor_sym, idx.table, idx.row, $val_expr)))
    end

    push!(exprs, Expr(:return, :nothing))

    return quote
        @inbounds begin
            $(Expr(:block, exprs...))
        end
    end
end

"""
    new_entity!(world::World, values::Tuple; relations::Tuple=())::Entity

Creates a new [`Entity`](@ref) with the given component values. Types are inferred from the values.

# Arguments

  - `world::World`: The `World` instance to use.
  - `values::Tuple`: Component values for the entity.
  - `defaults::Tuple`: A tuple of default values for initialization, like `(Position(0, 0), Velocity(1, 1))`.
  - `relations::Tuple`: Relationship component type => target entity pairs.

# Examples

Create an entity with components:

```jldoctest; setup = :(using Ark; include(string(dirname(pathof(Ark)), "/docs.jl"))), output = false
entity = new_entity!(world, (Position(0, 0), Velocity(1, 1)))

# output

Entity(4, 0)
```

Create an entity with components and relationships:

```jldoctest; setup = :(using Ark; include(string(dirname(pathof(Ark)), "/docs.jl"))), output = false
entity = new_entity!(world, (Position(0, 0), ChildOf()); relations=(ChildOf => parent,))

# output

Entity(4, 0)
```
"""
Base.@constprop :aggressive function new_entity!(
    world::World,
    values::Tuple;
    relations::Tuple{Vararg{Pair{DataType,Entity}}}=(),
)
    rel_types = ntuple(i -> Val(relations[i].first), length(relations))
    targets = ntuple(i -> relations[i].second, length(relations))

    entity, table_id = _new_entity!(world, Val{typeof(values)}(), values, rel_types, targets)
    table = world._tables[table_id]
    if _has_observers(world._event_manager, OnCreateEntity)
        _fire_create_entity(world._event_manager, entity, world._archetypes[table.archetype].mask)
    end
    return entity
end

@generated function _new_entity!(
    world::W,
    ::Val{TS},
    values::Tuple,
    ::TR,
    targets::Tuple{Vararg{Entity}},
) where {W<:World,TS<:Tuple,TR<:Tuple}
    types = TS.parameters
    rel_types = _to_types(TR)
    exprs = []

    ids = tuple([_component_id(W.parameters[1], T) for T in types]...)
    rel_ids = tuple([_component_id(W.parameters[1], T) for T in rel_types]...)

    # TODO: check relation components are actually relations

    push!(exprs, :(table = _find_or_create_table!(world, world._tables[1], $ids, (), $rel_ids, targets)))
    push!(exprs, :(tmp = _create_entity!(world, table)))
    push!(exprs, :(entity = tmp[1]))
    push!(exprs, :(index = tmp[2]))

    # Set each component
    for i in 1:length(types)
        T = types[i]
        stor_sym = Symbol("stor", i)
        col_sym = Symbol("col", i)
        val_expr = :(values.$i)

        push!(exprs, :($stor_sym = _get_storage(world, $T)))
        push!(exprs, :(@inbounds $col_sym = $stor_sym.data[table]))
        push!(exprs, :(@inbounds $col_sym[index] = $val_expr))
    end

    push!(exprs, Expr(:return, Expr(:tuple, :entity, :table)))

    return quote
        @inbounds begin
            $(Expr(:block, exprs...))
        end
    end
end

"""
    copy_entity!(
        world::World,
        entity::Entity;
        add::Tuple=(),
        remove::Tuple=(),
        mode=:copy,
    )

Copies an [`Entity`](@ref), optionally adding and/or removing components.

Mutable and non-isbits components are shallow copied by default. This can be changed with the `mode` argument.

# Arguments

  - `world`: The `World` instance to query.
  - `entity::Entity`: The entity to copy.
  - `add::Tuple`: Components to add, like `with=(Health(0),)`.
  - `remove::Tuple`: Component types to remove, like `(Position,Velocity)`.
  - `mode::Tuple`: Copy mode for mutable and non-isbits components. Modes are :ref, :copy, :deepcopy.

# Examples

Simple copy of an entity:

```jldoctest; setup = :(using Ark; include(string(dirname(pathof(Ark)), "/docs.jl"))), output = false
entity1 = copy_entity!(world, entity)

# output

Entity(4, 0)
```

Copy an entity, adding and removing some components in the same operation:

```jldoctest; setup = :(using Ark; include(string(dirname(pathof(Ark)), "/docs.jl"))), output = false
entity2 = copy_entity!(world, entity;
    add=(Health(100),),
    remove=(Position, Velocity),
)

# output

Entity(4, 0)
```
"""
@inline Base.@constprop :aggressive function copy_entity!(
    world::World, entity::Entity;
    add::Tuple=(), remove::Tuple=(),
    mode::Symbol=:copy,
)
    if !is_alive(world, entity)
        throw(ArgumentError("can't copy a dead entity"))
    end
    if isempty(add) && isempty(remove)
        return @inline _copy_entity!(world, entity, Val(mode))
    end
    return @inline _copy_entity!(
        world,
        entity,
        Val{typeof(add)}(),
        add,
        ntuple(i -> Val(remove[i]), length(remove)),
        Val(mode),
    )
end

"""
    new_entities!(world::World, n::Int, defaults::Tuple; iterate::Bool=false)::Union{Batch,Nothing}

Creates the given number of [`Entity`](@ref), initialized with default values.
Component types are inferred from the provided default values.

If `iterate` is true, a [`Batch`](@ref) iterator over the newly created entities is returned
that can be used for initialization.

See also [new_entities!](@ref new_entities!(::World, ::Int, ::Tuple)) for creating entities from component types.

# Arguments

  - `world::World`: The `World` instance to use.
  - `n::Int`: The number of entities to create.
  - `defaults::Tuple`: A tuple of default values for initialization, like `(Position(0, 0), Velocity(1, 1))`.
  - `iterate::Bool`: Whether to return a batch for individual entity initialization.

# Examples

Create 100 entities from default values:

```jldoctest; setup = :(using Ark; include(string(dirname(pathof(Ark)), "/docs.jl"))), output = false
new_entities!(world, 100, (Position(0, 0), Velocity(1, 1)))

# output

```

Create 100 entities from default values and iterate them:

```jldoctest; setup = :(using Ark; include(string(dirname(pathof(Ark)), "/docs.jl"))), output = false
for (entities, positions, velocities) in new_entities!(world, 100, (Position(0, 0), Velocity(1, 1)); iterate=true)
    for i in eachindex(entities)
        positions[i] = Position(rand(), rand())
    end
end

# output

```
"""
Base.@constprop :aggressive function new_entities!(world::World, n::Int, defaults::Tuple; iterate::Bool=false)
    return _new_entities_from_defaults!(world, UInt32(n), Val{typeof(defaults)}(), defaults, iterate)
end

@generated function _new_entities_from_defaults!(
    world::W,
    n::UInt32,
    ::Val{TS},
    values::Tuple,
    iterate::Bool,
) where {W<:World,TS<:Tuple}
    types = TS.parameters
    exprs = []

    ids = tuple([_component_id(W.parameters[1], T) for T in types]...)

    push!(exprs, :(table_idx = _find_or_create_table!(world, world._tables[1], $ids, (), (), ())))
    push!(exprs, :(indices = _create_entities!(world, table_idx, n)))
    push!(exprs, :(table = world._tables[table_idx]))

    if length(types) > 0
        body_exprs = Expr(:block)
        for i in 1:length(types)
            T = types[i]
            stor_sym = Symbol("stor", i)
            col_sym = Symbol("col", i)
            val_expr = :(values.$i)

            push!(body_exprs.args, :($stor_sym = _get_storage(world, $T)))
            push!(body_exprs.args, :(@inbounds $col_sym = $stor_sym.data[table_idx]))
            push!(body_exprs.args, :(fill!(view($col_sym, Int(indices[1]):Int(indices[2])), $val_expr)))
        end
        push!(exprs, :(
            if !isempty(values)
                $(body_exprs)
            end
        ))
    end

    types_tuple_type_expr = Expr(:curly, :Tuple, [:($T) for T in types]...)
    ts_val_expr = :(Val{$(types_tuple_type_expr)}())
    push!(
        exprs,
        :(
            if iterate
                batch = _Batch_from_types(
                    world,
                    [_BatchTable(table, world._archetypes[table.archetype], indices...)],
                    $ts_val_expr,
                )
                return batch
            else
                if _has_observers(world._event_manager, OnCreateEntity)
                    l = _lock(world._lock)
                    _fire_create_entities(
                        world._event_manager,
                        _BatchTable(table, world._archetypes[table.archetype], indices...),
                    )
                    _unlock(world._lock, l)
                end
                return nothing
            end
        ),
    )

    return quote
        @inbounds begin
            $(Expr(:block, exprs...))
        end
    end
end

"""
    new_entities!(world::World, n::Int, comp_types::Tuple)::Batch

Creates the given number of [`Entity`](@ref).

Returns a [`Batch`](@ref) iterator over the newly created entities that should be used to initialize components.
Note that components are not initialized/undef unless set in the iterator!

See also [new_entities!](@ref new_entities!(::World, ::Int, ::Tuple; ::Bool)) for creating entities from default values.

# Arguments

  - `world::World`: The `World` instance to use.
  - `n::Int`: The number of entities to create.
  - `comp_types::Tuple`: Component types for the new entities, like `(Position, Velocity)`.

# Example

Create 100 entities from component types and initialize them:

```jldoctest; setup = :(using Ark; include(string(dirname(pathof(Ark)), "/docs.jl"))), output = false
for (entities, positions, velocities) in new_entities!(world, 100, (Position, Velocity))
    for i in eachindex(entities)
        positions[i] = Position(rand(), rand())
        velocities[i] = Velocity(1, 1)
    end
end

# output

```
"""
Base.@constprop :aggressive function new_entities!(world::World, n::Int, comp_types::Tuple{Vararg{DataType}})
    return _new_entities_from_types!(world, UInt32(n), ntuple(i -> Val(comp_types[i]), length(comp_types)))
end

@generated function _new_entities_from_types!(world::W, n::UInt32, ::TS) where {W<:World,TS<:Tuple}
    types = _to_types(TS)
    exprs = []

    ids = tuple([_component_id(W.parameters[1], T) for T in types]...)

    push!(exprs, :(table_idx = _find_or_create_table!(world, world._tables[1], $ids, (), (), ())))
    push!(exprs, :(indices = _create_entities!(world, table_idx, n)))
    push!(exprs, :(table = world._tables[table_idx]))

    types_tuple_type_expr = Expr(:curly, :Tuple, [:($T) for T in types]...)
    ts_val_expr = :(Val{$(types_tuple_type_expr)}())
    push!(exprs,
        :(
            batch = _Batch_from_types(
                world,
                [_BatchTable(table, world._archetypes[table.archetype], indices[1], indices[2])],
                $ts_val_expr)
        ),
    )

    push!(exprs, Expr(:return, :batch))

    return quote
        @inbounds begin
            $(Expr(:block, exprs...))
        end
    end
end

"""
    add_components!(world::World, entity::Entity, values::Tuple)

Adds the given component values to an [`Entity`](@ref). Types are inferred from the values.

# Example

```jldoctest; setup = :(using Ark; include(string(dirname(pathof(Ark)), "/docs.jl"))), output = false
add_components!(world, entity, (Health(100),))

# output

```
"""
@inline Base.@constprop :aggressive function add_components!(world::World, entity::Entity, values::Tuple)
    if !is_alive(world, entity)
        throw(ArgumentError("can't add components to a dead entity"))
    end
    return @inline _exchange_components!(world, entity, Val{typeof(values)}(), values, ())
end

"""
    remove_components!(world::World, entity::Entity, comp_types::Tuple)

Removes the given components from an [`Entity`](@ref).

# Example

```jldoctest; setup = :(using Ark; include(string(dirname(pathof(Ark)), "/docs.jl"))), output = false
remove_components!(world, entity, (Position, Velocity))

# output

```
"""
@inline Base.@constprop :aggressive function remove_components!(world::World, entity::Entity, comp_types::Tuple)
    if !is_alive(world, entity)
        throw(ArgumentError("can't remove components from a dead entity"))
    end
    return @inline _exchange_components!(
        world,
        entity,
        Val{Tuple{}}(),
        (),
        ntuple(i -> Val(comp_types[i]), length(comp_types)),
    )
end

"""
    exchange_components!(world::World{CS,CT,N}, entity::Entity; add::Tuple, remove::Tuple)

Adds and removes components on an [`Entity`](@ref). Types are inferred from the add values.

# Example

```jldoctest; setup = :(using Ark; include(string(dirname(pathof(Ark)), "/docs.jl"))), output = false
exchange_components!(world, entity;
    add=(Health(100),),
    remove=(Position, Velocity),
)

# output

```
"""
@inline Base.@constprop :aggressive function exchange_components!(
    world::World,
    entity::Entity;
    add::Tuple=(),
    remove::Tuple=(),
)
    if !is_alive(world, entity)
        throw(ArgumentError("can't exchange components on a dead entity"))
    end
    return @inline _exchange_components!(
        world,
        entity,
        Val{typeof(add)}(),
        add,
        ntuple(i -> Val(remove[i]), length(remove)),
    )
end

@generated function _exchange_components!(
    world::W,
    entity::Entity,
    ::Val{ATS},
    add::Tuple,
    ::RTS,
) where {W<:World,ATS<:Tuple,RTS<:Tuple}
    add_types = ATS.parameters
    rem_types = _to_types(RTS)

    if isempty(add_types) && isempty(rem_types)
        throw(ArgumentError("either components to add or to remove must be given for exchange_components!"))
    end

    exprs = []

    add_ids = tuple([_component_id(W.parameters[1], T) for T in add_types]...)
    rem_ids = tuple([_component_id(W.parameters[1], T) for T in rem_types]...)

    push!(exprs, :(index = world._entities[entity._id]))
    push!(exprs, :(old_table = world._tables[index.table]))
    push!(
        exprs,
        :(
            new_table_index =
                _find_or_create_table!(
                    world, old_table, $add_ids, $rem_ids, (), (),
                )
        ),
    )
    push!(exprs, :(new_table = world._tables[new_table_index]))

    if length(rem_types) > 0
        push!(
            exprs,
            :(
                if _has_observers(world._event_manager, OnRemoveComponents)
                    l = _lock(world._lock)
                    _fire_remove_components(
                        world._event_manager, entity,
                        world._archetypes[old_table.archetype].mask,
                        world._archetypes[new_table.archetype].mask,
                        true,
                    )
                    _unlock(world._lock, l)
                end
            ),
        )
    end

    push!(exprs, :(row = _move_entity!(world, entity, new_table_index)))

    for i in 1:length(add_types)
        T = add_types[i]
        stor_sym = Symbol("stor", i)
        col_sym = Symbol("col", i)
        val_expr = :(add.$i)

        push!(exprs, :($stor_sym = _get_storage(world, $T)))
        push!(exprs, :(@inbounds $col_sym = $stor_sym.data[new_table_index]))
        push!(exprs, :(@inbounds $col_sym[row] = $val_expr))
    end

    if length(add_types) > 0
        push!(
            exprs,
            :(
                if _has_observers(world._event_manager, OnAddComponents)
                    _fire_add_components(
                        world._event_manager, entity,
                        world._archetypes[old_table.archetype].mask,
                        world._archetypes[new_table.archetype].mask,
                        true,
                    )
                end
            ),
        )
    end

    push!(exprs, Expr(:return, :nothing))

    return quote
        @inbounds begin
            $(Expr(:block, exprs...))
        end
    end
end

@generated function _World_from_types(
    ::Val{CS},
    ::Val{ST},
    ::Val{MUT},
    initial_capacity::Int,
) where {CS<:Tuple,ST<:Tuple,MUT}
    types = CS.parameters
    storage_val_types = ST.parameters
    allow_mutable = MUT::Bool

    for (T, mode) in zip(types, storage_val_types)
        if !isconcretetype(T)
            throw(
                ArgumentError("can't use $(nameof(T)) as component as it is not a concrete type"),
            )
        end
        if !(mode <: StructArrayStorage || mode <: VectorStorage)
            throw(
                ArgumentError(
                    "$(nameof(mode)) is not a valid storage mode, must be StructArrayStorage or VectorStorage",
                ),
            )
        end
        if mode <: StructArrayStorage && fieldcount(T) == 0
            throw(
                ArgumentError("can't use StructArrayStorage for $(nameof(T)) because it has no fields"),
            )
        end
    end

    # Immutability checks
    for (T, mode) in zip(types, storage_val_types)
        if ismutabletype(T)
            if mode <: StructArrayStorage
                throw(
                    ArgumentError("Component type $(nameof(T)) must be immutable because it uses StructArray storage"),
                )
            elseif !allow_mutable
                throw(ArgumentError("Component type $(nameof(T)) must be immutable unless 'allow_mutable' is used"))
            end
        end
    end

    # Component type tuple
    component_types = map(T -> :(Type{$T}), types)
    component_tuple_type = :(Tuple{$(component_types...)})

    # Storage type logic (based on resolved Val{...} types)
    storage_types = Vector{Any}(undef, length(types))
    storage_exprs = Vector{Any}(undef, length(types))

    for i in 1:length(types)
        T = types[i]
        mode = storage_val_types[i]
        if mode <: StructArrayStorage
            storage_types[i] = :(_ComponentStorage{$T,_StructArray_type($T)})
            storage_exprs[i] = :(_new_struct_array_storage($T))
        else
            storage_types[i] = :(_ComponentStorage{$T,Vector{$T}})
            storage_exprs[i] = :(_new_vector_storage($T))
        end
    end

    # Final type and value tuples
    storage_tuple_type = :(Tuple{$(storage_types...)})
    storage_tuple = Expr(:tuple, storage_exprs...)

    storage_mode_type = :(Tuple{$(storage_val_types...)})

    # Component registration
    id_exprs = [:(_register_component!(registry, $T)) for T in types]
    id_tuple = Expr(:tuple, id_exprs...)

    relations_expr = [:(_new_component_relations($T <: Relationship)) for T in types]
    relations_vec = Expr(:vect, relations_expr...)

    M = max(1, cld(length(types), 64))
    return quote
        registry = _ComponentRegistry()
        ids = $id_tuple
        graph = _Graph{$(M)}()
        index = _EntityIndex[_EntityIndex(typemax(UInt32), 0)]
        sizehint!(index, initial_capacity)

        World{$(storage_tuple_type),$(component_tuple_type),$(storage_mode_type),$(length(types)),$M}(
            index,
            $storage_tuple,
            $relations_vec,
            [_Archetype(UInt32(1), first(graph.nodes)[2], _TableIDs(1))],
            [_new_table(UInt32(1), UInt32(1))],
            _ComponentIndex{$(M)}($(length(types))),
            registry,
            _EntityPool(UInt32(1024)),
            _Lock(),
            graph,
            Dict{DataType,Any}(),
            _EventManager{
                World{$(storage_tuple_type),$(component_tuple_type),$(storage_mode_type),$(length(types)),$M},
                $(M),
            }(),
            Vector{Pair{Int,Entity}}(),
            initial_capacity,
        )
    end
end

@generated function _push_empty_to_all_storages!(world::World{CS,CT}) where {CS<:Tuple,CT<:Tuple}
    comp_types = CT.parameters
    n = length(comp_types)
    exprs = Expr[]
    for i in 1:n
        push!(exprs, :(_add_column!(world._storages.$i)))
    end
    return Expr(:block, exprs...)
end

@generated function _push_zero_to_all_relations!(world::World{CS,CT}) where {CS<:Tuple,CT<:Tuple}
    comp_types = CT.parameters
    n = length(comp_types)
    exprs = Expr[]
    for i in 1:n
        if comp_types[i].parameters[1] <: Relationship
            push!(exprs, :(_add_column!(world._relations[$i])))
        end
    end
    return Expr(:block, exprs...)
end

@generated function _activate_new_column_for_comp!(world::World{CS}, comp::Int, index::Int) where {CS}
    n = length(CS.parameters)
    exprs = Expr[]
    for i in 1:n
        push!(exprs, :(
            if comp == $i
                _activate_column!(world._storages.$i, index, world._initial_capacity)
            end
        ))
    end
    return Expr(:block, exprs...)
end

@generated function _activate_relation_for_comp!(world::World{CS,CT}, comp::Int, arch::Int, index::Int) where {CS,CT}
    comp_types = CT.parameters
    n = length(comp_types)
    exprs = Expr[]
    for i in 1:n
        if !(comp_types[i].parameters[1] <: Relationship)
            continue
        end
        push!(exprs, :(
            if comp == $i
                _activate_column!(world._relations[$i], arch, index)
            end
        ))
    end
    return Expr(:block, exprs...)
end

@generated function _ensure_column_size_for_comp!(
    world::World{CS},
    comp::Int,
    arch::UInt32,
    needed::Int,
) where {CS<:Tuple}
    n = length(CS.parameters)
    exprs = Expr[]
    for i in 1:n
        push!(exprs, :(
            if comp == $i
                _ensure_column_size!(world._storages.$i, arch, needed)
            end
        ))
    end
    return Expr(:block, exprs...)
end

@generated function _move_component_data!(
    world::World{CS},
    comp::Int,
    old_arch::UInt32,
    new_arch::UInt32,
    row::UInt32,
) where {CS<:Tuple}
    n = length(CS.parameters)
    exprs = Expr[]
    for i in 1:n
        push!(exprs, :(
            if comp == $i
                _move_component_data!(world._storages.$i, old_arch, new_arch, row)
            end
        ))
    end
    return Expr(:block, exprs...)
end

@generated function _copy_component_data!(
    world::World{CS},
    comp::Int,
    old_arch::UInt32,
    new_arch::UInt32,
    old_row::UInt32,
    new_row::UInt32,
    mode::CP,
) where {CS<:Tuple,CP<:Val}
    if !(CP in [Val{:ref}, Val{:copy}, Val{:deepcopy}])
        mode = CP.parameters[1]
        throw(ArgumentError(":$mode is not a valid copy mode, must be :ref, :copy or :deepcopy"))
    end

    n = length(CS.parameters)
    exprs = Expr[]
    for i in 1:n
        push!(exprs, :(
            if comp == $i
                _copy_component_data!(world._storages.$i, old_arch, new_arch, old_row, new_row, mode)
            end
        ))
    end
    return Expr(:block, exprs...)
end

@generated function _clear_component_data!(
    world::World{CS},
    comp::Int,
    arch::UInt32,
) where {CS<:Tuple}
    n = length(CS.parameters)
    exprs = Expr[]
    for i in 1:n
        push!(exprs, :(
            if comp == $i
                _clear_column!(world._storages.$i, arch)
            end
        ))
    end
    return Expr(:block, exprs...)
end

@generated function _swap_remove_in_column_for_comp!(
    world::World{CS},
    comp::Int,
    arch::UInt32,
    row::UInt32,
) where {CS<:Tuple}
    n = length(CS.parameters)
    exprs = Expr[]
    for i in 1:n
        push!(exprs, :(
            if comp == $i
                _remove_component_data!(world._storages.$i, arch, row)
            end
        ))
    end
    return Expr(:block, exprs...)
end

"""
    get_resource(world::World, res_type::Type{T})::T

Get the resource of type `T` from the world.
"""
function get_resource(world::World, res_type::Type{T})::T where T
    getindex(world._resources, res_type)::T
end

"""
    has_resource(world::World, res_type::Type{T})::Bool

Check if a resource of type `T` is in the world.
"""
function has_resource(world::World, res_type::Type)::Bool
    res_type in keys(world._resources)
end

"""
    add_resource!(world::World, res::T)::T

Add the given resource to the world.
Returns the newly added resource.
"""
function add_resource!(world::World, res::T)::T where T
    has_resource(world, T) && throw(ArgumentError(lazy"World already contains a resource of type $T"))
    setindex!(world._resources, res, T)
    return res
end

"""
    set_resource!(world::World, res::T)::T

Overwrites an existing resource in the world.
Returns the newly overwritten resource.
"""
function set_resource!(world::World, res::T)::T where T
    !has_resource(world, T) && throw(ArgumentError(lazy"World does not contain a resource of type $T"))
    setindex!(world._resources, res, T)
    return res
end

"""
    remove_resource!(world::World, res_type::Type{T})::T

Remove the resource of type `T` from the world.
Returns the removed resource.
"""
function remove_resource!(world::World, res_type::Type{T}) where T
    res = pop!(world._resources, res_type)
    return res::T
end

"""
    emit_event!(world::World, event::EventType, entity::Entity, components::Tuple=())

Emits a custom event for the given [EventType](@ref), [Entity](@ref) and optional components.
The entity must have the given components. The entity can be the reserved [zero_entity](@ref).

  - `world::World`: The [World](@ref) to emit the event.
  - `event::EventType`: The [EventType](@ref) to emit.
  - `entity::Entity`: The [Entity](@ref) to emit the event for.
  - `components::Tuple=()`: The component types to emit the event for. Optional.

# Example

```jldoctest; setup = :(using Ark; include(string(dirname(pathof(Ark)), "/docs.jl"))), output = false
emit_event!(world, OnCollisionDetected, entity, (Position, Velocity))

# output

```
"""
@inline Base.@constprop :aggressive function emit_event!(
    world::W,
    event::EventType,
    entity::Entity,
    components::Tuple=(),
) where {W<:World}
    if event._id < _custom_events._id
        throw(ArgumentError("only custom events can be emitted manually"))
    end
    if !_has_observers(world._event_manager, event)
        return
    end
    _emit_event!(world, event, entity, ntuple(i -> Val(components[i]), length(components)))
end

@generated function _emit_event!(world::W, event::EventType, entity::Entity, ::CT) where {W<:World,CT<:Tuple}
    comp_types = [x.parameters[1] for x in CT.parameters]

    CS = W.parameters[1]
    has_comps = (length(comp_types) > 0) ? :(true) : (false)
    ids = map(C -> _component_id(CS, C), comp_types)
    M = max(1, cld(length(CS.parameters), 64))
    mask = _Mask{M}(ids...)

    return quote
        _do_emit_event!(world, event, $mask, $has_comps, entity)
    end
end

function _do_emit_event!(world::World, event::EventType, mask::_Mask, has_comps::Bool, entity::Entity)
    if is_zero(entity)
        if has_comps
            throw(ArgumentError("can't emit event with components for the zero entity"))
        end
        return _fire_custom_event(world._event_manager, entity, event, mask, world._archetypes[1].mask)
    end

    if !is_alive(world, entity)
        throw(ArgumentError("can't emit event for a dead entity"))
    end
    index = world._entities[entity._id]
    table = world._tables[index.table]
    entity_mask = world._archetypes[table.archetype].mask

    if !_contains_all(entity_mask, mask)
        throw(ArgumentError("entity does not have all components of the event emitted for it"))
    end
    _fire_custom_event(world._event_manager, entity, event, mask, entity_mask)
end

"""
    reset!(world::World)

Removes all entities and resources from the world, and un-registers all observers.
Does NOT free reserved memory or remove archetypes.

Can be used to run systematic simulations without the need to re-allocate memory for each run.
Accelerates re-populating the world by a factor of 2-3.
"""
function reset!(world::W) where {W<:World}
    _check_locked(world)

    resize!(world._entities, 1)
    _reset!(world._entity_pool)
    _reset!(world._lock)
    _reset!(world._event_manager)

    for table in world._tables
        resize!(table, 0)
        archetype = world._archetypes[table.archetype]
        for comp in archetype.components
            _clear_component_data!(world, comp, table.id)
        end
    end

    empty!(world._resources)
    return nothing
end

function Base.show(io::IO, world::World{CS,CT}) where {CS<:Tuple,CT<:Tuple}
    comp_types = CT.parameters
    type_names = join(map(_format_type, comp_types), ", ")
    entities = sum(length(arch.entities) for arch in world._tables)
    print(io, "World(entities=$entities, comp_types=($type_names))")
end
